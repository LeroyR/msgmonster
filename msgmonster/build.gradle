plugins {
    id 'java'
    id 'com.diffplug.spotless'
    id 'maven-publish'
    id 'application'
}

dependencies {
    implementation 'io.github.lambdaprime:id.xfunction:19.0'
    implementation 'org.ainslec:picocog:1.0.7'

    // transitively export following dependency to tests project
    apiElements 'io.github.lambdaprime:id.xfunction:19.0'
    apiElements 'org.ainslec:picocog:1.0.7'
}

jar { 
    manifest { 
        attributes 'Implementation-Version': "${rootProject.version}",
                   'Main-Class': 'id.msgmonster.app.MsgmonsterApp'
    }
}

application {
    mainModule = 'id.msgmonster.app' // name defined in module-info.java
    mainClass = 'id.msgmonster.app.MsgmonsterApp'
}

def packageDir = "$buildDir/package/${projectName}"

ext.artifacts = fileTree(include: ['*'], dir: "$packageDir/libs")

task createRelease(type: Zip) {
    archiveFileName = "$releaseFileName"
    destinationDirectory = file("$projectDir/release")
    from file("$packageDir/..")
}

task copyLibs(type: Copy) {
    from configurations.runtimeClasspath
    from "$projectDir/libs"
    from jar.archiveFile
    into "${packageDir}/libs"
}

task copyReadme(type: Copy) {
    from("${project.rootDir}/README.md")
    into("$projectDir/src/main/resources")
}

task createPackage(type: Copy, dependsOn: copyLibs) {
    from "${project.rootDir}/README.md"
    from "${project.rootDir}/LICENSE-2.0.txt"
    from "$projectDir/src/main/msgmonster"
    into packageDir
}

task cleanRelease(type: Delete) {
    delete "$packageDir"
    delete "$projectDir/release"
}

installDist {
    destinationDir rootProject.file("build/install")
}

compileJava.dependsOn copyReadme
createRelease.dependsOn createPackage
assemble.dependsOn createRelease
clean.dependsOn cleanRelease
processResources.dependsOn copyReadme

// run spotless and format code before the build
classes.dependsOn spotlessApply

String mavenDeploymentRepository = System.getenv("ROS_MAVEN_DEPLOYMENT_REPOSITORY")
if (mavenDeploymentRepository != null &&
    mavenDeploymentRepository != "") {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = 'pinorobotics'
                from components.java
                artifact distZip
            }
        }
        repositories {
            maven {
                url 'file://' + mavenDeploymentRepository
            }
        }
    }
} else {
    logger.error("ROS_MAVEN_DEPLOYMENT_REPOSITORY is not set. Have you sourced setup.bash?")
}
